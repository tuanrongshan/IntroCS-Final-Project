class NormalStair {

    static int W, H; // Constant int width and height
    static int frameGap; // Constant int minimum gap length from frame to stairs
    static int boundRight;
    static int yShift;
    static int respeed; //the reciprocal of speed of rising stairs
    field int xPos, yPos; // Screen location of the stair's top-left corner
    field int tick; // internal clock
    // field int style; //1:normal 2:spring 3:spikes

    //score: the number of stairs the player passed?

    // Initialize global values W, H
    function void init(int irespeed) {
        let W = 70;
        let H = 10;
        let frameGap = 50;
        let respeed = irespeed;
        let boundRight = 300-W;
        let yShift = 16;
        return;
    }

    // Up shift stairs
    // function void shift(int s) {
    //     let yShift = s;
    //     return;
    // }

    // Construct a new Normal Stair
    constructor NormalStair new(int y) {
        do reset(y);
        return this;
    }

    // Dispose this normal stair
    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }

    // Draw stair
    method void draw() {
        var int t, d;
        if (yPos < 512){
            do Screen.setColor(true);
            let t = Math.min(Math.max(yPos, 0),255);
            let d = Math.min(Math.max(yPos+H, 0),255);
            // do Screen.drawRectangle(xPos, yPos, xPos+W, yPos+H);
            do Screen.drawRectangle(xPos, t, xPos+W, d);
        }
        return;
    }

    // Clear stair
    method void clear() {
        var int t, d;
        do Screen.setColor(false);
        let t = Math.min(Math.max(yPos, 0),255);
        let d = Math.min(Math.max(yPos+H, 0),255);
        // do Screen.drawRectangle(xPos, yPos, xPos+W, yPos+H);
        do Screen.drawRectangle(xPos, t, xPos+W, d);
        return;
    }

    // Update new state
    method void update() {
        let tick = tick + 1;
        if (tick = respeed) {let tick = 0;}
        // if ((tick = 0) & (yShift > 0)) {
        if (tick = 0){
            do clear();
            let yPos = yPos - yShift;
            // let yShift = 0;
            if (yPos < 10) {
                // let coin = true;
                let yPos = 258;
                let xPos = LCGRandom.randRange(0, boundRight);
            }
        }
        do draw();
        return;
    }

    // Reset
    method void reset(int y) {
        // let xPos = LCGRandom.randRange(frameGap, 500-W-frameGap);
        let xPos = LCGRandom.randRange(0, boundRight);
        let yPos = y;
        let tick = 0;
        do draw();
        return;
    }

    method int stand_on(int x1, int x2, int y) {
        // y:the bottom of the player
        // x1/x2:left & right
        if ((y = (yPos+1)) & (x2>xPos) & (x1<(xPos+W))){
            return 1; //normal stair
        } else {
            return 0;
        }
    }
}